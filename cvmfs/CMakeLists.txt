#
# Define the sources needed to compile the different
# executables in this directory
#

set (CVMFS_LOADER_SOURCES
  atomic.h
  fence.h
  fs_traversal.h
  globals.cc globals.h
  hash.cc hash.h
  loader.cc loader.h
  loader_talk.cc loader_talk.h
  logging.cc logging.h logging_internal.h
  options.cc options.h
  platform.h platform_linux.h platform_osx.h
  prng.h
  sanitizer.cc sanitizer.h
  shortstring.h
  smalloc.h
  statistics.cc statistics.h
  util/pointer.h
  util/posix.cc util/posix.h
  util/single_copy.h
  util/string.cc util/string.h
)

set (CVMFS_CLIENT_SOURCES
  atomic.h
  backoff.cc backoff.h
  bigvector.h
  cache.cc cache.h
  catalog.cc catalog.h
  catalog_counters.cc catalog_counters.h catalog_counters_impl.h
  catalog_mgr.h catalog_mgr_impl.h
  catalog_mgr_client.cc catalog_mgr_client.h
  catalog_sql.cc catalog_sql.h
  clientctx.cc clientctx.h
  compression.cc compression.h
  directory_entry.cc directory_entry.h
  dns.cc dns.h
  download.cc download.h
  duplex_cares.h
  duplex_curl.h
  duplex_sqlite3.h
  duplex_zlib.h
  fetch.cc fetch.h
  file_chunk.cc file_chunk.h
  fs_traversal.h
  globals.cc globals.h
  hash.cc hash.h
  logging.cc logging.h logging_internal.h
  lru.h
  manifest.cc manifest.h
  manifest_fetch.cc manifest_fetch.h
  monitor.cc monitor.h
  murmur.h
  platform.h platform_linux.h platform_osx.h
  prng.h
  quota.cc quota.h
  sanitizer.cc sanitizer.h
  shortstring.h
  signature.cc signature.h
  sink.h
  smallhash.h
  smalloc.h
  sql.cc sql.h sql_impl.h
  sqlitemem.cc sqlitemem.h
  sqlitevfs.cc sqlitevfs.h
  statistics.cc statistics.h
  uid_map.h
  uuid.cc uuid.h
  util/algorithm.cc util/algorithm.h
  util/plugin.h
  util/pointer.h
  util/posix.cc util/posix.h
  util/single_copy.h
  util/string.cc util/string.h
  util/unlink_guard.h
  whitelist.cc whitelist.h
  wpad.cc wpad.h
  xattr.cc xattr.h
)
if (VOMS_FOUND)
  set (CVMFS_CLIENT_SOURCES ${CVMFS_CLIENT_SOURCES}
  voms_authz/voms_authz.cc voms_authz/voms_authz.h
  voms_authz/voms_cred.cc voms_authz/voms_cred.h
  voms_authz/voms_curl.cc)
endif (VOMS_FOUND)


set (CVMFS2_DEBUG_SOURCES
  ${CVMFS_CLIENT_SOURCES}

  auto_umount.cc auto_umount.h
  compat.cc compat.h
  cvmfs.cc cvmfs.h
  fence.h
  glue_buffer.cc glue_buffer.h
  history.h
  history_sql.cc history_sql.h
  history_sqlite.cc history_sqlite.h
  loader.h
  nfs_maps.cc nfs_maps.h
  nfs_shared_maps.cc nfs_shared_maps.h
  options.cc options.h
  quota_listener.cc quota_listener.h
  talk.cc talk.h
  tracer.cc tracer.h
)
if (VOMS_FOUND)
  set (CVMFS2_DEBUG_SOURCES ${CVMFS2_DEBUG_SOURCES}
  voms_authz/cred_fetcher.cc)
endif (VOMS_FOUND)

set (CVMFS2_SOURCES
  ${CVMFS2_DEBUG_SOURCES}
)

set (LIBCVMFS_SOURCES
  ${CVMFS_CLIENT_SOURCES}

  libcvmfs.cc libcvmfs.h
  libcvmfs_int.cc libcvmfs_int.h
)

set (TEST_LIBCVMFS_SOURCES
  libcvmfs.h
  test_libcvmfs.cc
)

set (CVMFS_FSCK_SOURCES
  atomic.h
  compression.cc compression.h
  cvmfs_fsck.cc
  duplex_zlib.h
  hash.cc hash.h
  logging.cc logging.h logging_internal.h
  platform.h platform_linux.h platform_osx.h
  prng.h
  smalloc.h
  statistics.cc statistics.h
  util/plugin.h
  util/pointer.h
  util/posix.cc util/posix.h
  util/single_copy.h
  util/string.cc util/string.h
)

set (CVMFS_SWISSKNIFE_SOURCES
  atomic.h
  catalog.cc catalog.h
  catalog_balancer.h catalog_balancer_impl.h
  catalog_counters.cc catalog_counters.h catalog_counters_impl.h
  catalog_mgr.h catalog_mgr_impl.h
  catalog_mgr_ro.cc catalog_mgr_ro.h
  catalog_mgr_rw.cc catalog_mgr_rw.h
  catalog_sql.cc catalog_sql.h
  catalog_traversal.h
  catalog_rw.cc catalog_rw.h
  clientctx.cc clientctx.h
  compression.cc compression.h
  directory_entry.cc directory_entry.h
  dns.cc dns.h
  download.cc download.h
  duplex_cares.h
  duplex_curl.h
  duplex_sqlite3.h
  duplex_zlib.h
  file_chunk.cc file_chunk.h
  file_processing/async_reader.cc file_processing/async_reader.h file_processing/async_reader_impl.h
  file_processing/char_buffer.h
  file_processing/chunk.cc file_processing/chunk.h
  file_processing/chunk_detector.cc file_processing/chunk_detector.h
  file_processing/file.cc file_processing/file.h
  file_processing/file_processor.cc file_processing/file_processor.h
  file_processing/io_dispatcher.cc file_processing/io_dispatcher.h
  file_processing/processor.cc file_processing/processor.h
  fs_traversal.h
  globals.cc globals.h
  hash.cc hash.h
  history.h
  history_sql.cc history_sql.h
  history_sqlite.cc history_sqlite.h
  json_document.cc json_document.h
  letter.cc letter.h
  logging.cc logging.h logging_internal.h
  manifest.cc manifest.h
  manifest_fetch.cc manifest_fetch.h
  object_fetcher.h
  options.cc options.h
  path_filters/dirtab.cc path_filters/dirtab.h
  path_filters/relaxed_path_filter.cc path_filters/relaxed_path_filter.h
  pathspec/pathspec.cc pathspec/pathspec.h
  pathspec/pathspec_pattern.cc pathspec/pathspec_pattern.h
  platform.h platform_linux.h platform_osx.h
  prng.h
  reflog.cc reflog.h
  reflog_sql.cc reflog_sql.h
  s3fanout.cc s3fanout.h
  sanitizer.cc sanitizer.h
  shortstring.h
  signature.cc signature.h
  smalloc.h
  sql.cc sql.h sql_impl.h
  sqlitemem.cc sqlitemem.h
  statistics.cc statistics.h
  swissknife.cc swissknife.h
  swissknife_check.cc swissknife_check.h
  swissknife_gc.cc swissknife_gc.h
  swissknife_graft.cc swissknife_graft.h
  swissknife_hash.cc swissknife_hash.h
  swissknife_history.cc swissknife_history.h
  swissknife_info.cc swissknife_info.h
  swissknife_letter.cc swissknife_letter.h
  swissknife_lsrepo.cc swissknife_lsrepo.h
  swissknife_main.cc
  swissknife_migrate.cc swissknife_migrate.h
  swissknife_pull.cc swissknife_pull.h
  swissknife_reflog.cc swissknife_reflog.h
  swissknife_scrub.cc swissknife_scrub.h
  swissknife_sign.cc swissknife_sign.h
  swissknife_sync.cc swissknife_sync.h
  swissknife_zpipe.cc swissknife_zpipe.h
  sync_item.cc sync_item.h
  sync_mediator.cc sync_mediator.h
  sync_union.cc sync_union.h
  uid_map.h
  upload.cc upload.h
  upload_facility.cc upload_facility.h
  upload_local.cc upload_local.h
  upload_s3.cc upload_s3.h
  upload_spooler_definition.cc upload_spooler_definition.h
  upload_spooler_result.h
  util/algorithm.cc util/algorithm.h
  util/async.h
  util/mmap_file.cc util/mmap_file.h
  util/plugin.h
  util/pointer.h
  util/posix.cc util/posix.h
  util/single_copy.h
  util/string.cc util/string.h
  util_concurrency.cc util_concurrency.h util_concurrency_impl.h
  whitelist.cc whitelist.h
  xattr.cc xattr.h
)

if (VOMS_FOUND)
  set (CVMFS_SWISSKNIFE_SOURCES ${CVMFS_SWISSKNIFE_SOURCES}
       voms_authz/voms_authz.h
       voms_authz/voms_cred.cc
       voms_authz/voms_curl.cc)
endif (VOMS_FOUND)


set (CVMFS_SWISSKNIFE_DEBUG_SOURCES
  ${CVMFS_SWISSKNIFE_SOURCES}
)

set (CVMFS_SUID_HELPER_SOURCES
  cvmfs_suid_helper.cc
  platform.h platform_linux.h platform_osx.h
  sanitizer.cc sanitizer.h
)

set (CVMFS_PRELOADER_SOURCES
  atomic.h
  catalog.cc catalog.h
  catalog_sql.cc catalog_sql.h
  compression.cc compression.h
  dns.cc dns.h
  download.cc download.h
  duplex_cares.h
  duplex_curl.h
  duplex_sqlite3.h
  file_processing/async_reader.cc file_processing/async_reader.h file_processing/async_reader_impl.h
  file_processing/char_buffer.h
  file_processing/chunk.cc file_processing/chunk.h
  file_processing/chunk_detector.cc file_processing/chunk_detector.h
  file_processing/file.cc file_processing/file.h
  file_processing/file_processor.cc file_processing/file_processor.h
  file_processing/io_dispatcher.cc file_processing/io_dispatcher.h
  file_processing/processor.cc file_processing/processor.h
  globals.cc globals.h
  hash.cc hash.h
  history_sql.cc history_sql.h
  history_sqlite.cc history_sqlite.h
  logging.cc logging.h logging_internal.h
  manifest.cc manifest.h
  manifest_fetch.cc manifest_fetch.h
  options.cc options.h
  path_filters/dirtab.cc path_filters/dirtab.h
  path_filters/relaxed_path_filter.cc path_filters/relaxed_path_filter.h
  pathspec/pathspec.cc pathspec/pathspec.h
  pathspec/pathspec_pattern.cc pathspec/pathspec_pattern.h
  preload.cc
  reflog.cc reflog.h
  reflog_sql.cc reflog_sql.h
  s3fanout.cc s3fanout.h
  sanitizer.cc sanitizer.h
  signature.cc signature.h
  smalloc.h
  sql.cc sql.h sql_impl.h
  sqlitemem.cc sqlitemem.h
  statistics.cc statistics.h
  swissknife.cc swissknife.h
  swissknife_pull.cc swissknife_pull.h
  upload.cc upload.h
  upload_facility.cc upload_facility.h
  upload_local.cc upload_local.h
  upload_s3.cc upload_s3.h
  upload_spooler_definition.cc upload_spooler_definition.h
  util/algorithm.cc util/algorithm.h
  util/buffer.h
  util/mmap_file.cc util/mmap_file.h
  util/pointer.h
  util/posix.cc util/posix.h
  util/single_copy.h
  util/string.cc util/string.h
  util_concurrency.cc util_concurrency.h
  whitelist.cc whitelist.h
  xattr.cc xattr.h
)

if (VOMS_FOUND)
  set (CVMFS_PRELOADER_SOURCES ${CVMFS_PRELOADER_SOURCES}
       voms_authz/voms_authz.h
       voms_authz/voms_cred.cc
       voms_authz/voms_curl.cc)
endif (VOMS_FOUND)

#
# configure some compiler flags for proper build
#

set (CVMFS_LOADER_CFLAGS "${CVMFS_LOADER_CFLAGS} -DCVMFS_CLIENT -DCVMFS_NAMESPACE_GUARD=loader")

set (CVMFS2_CFLAGS "${CVMFS2_CFLAGS} -DCVMFS_CLIENT -D_FILE_OFFSET_BITS=64 -fexceptions")
set (CVMFS2_DEBUG_CFLAGS "${CVMFS2_DEBUG_CFLAGS} ${CVMFS2_CFLAGS} -D_FILE_OFFSET_BITS=64 -O0 -DDEBUGMSG")

set (CVMFS_SWISSKNIFE_DEBUG_CFLAGS "${CVMFS_SWISSKNIFE_DEBUG_CFLAGS} -DDEBUGMSG -g")

set (CVMFS_FSCK_CFLAGS "${CVMFS_FSCK_CFLAGS} -DCVMFS_CLIENT")

set (LIBCVMFS_CFLAGS "${LIBCVMFS_CFLAGS} -D_FILE_OFFSET_BITS=64 -DCVMFS_CLIENT -DCVMFS_LIBCVMFS -fexceptions")

#
# set some include and linking stuff
#
set (CVMFS2_LD_FLAGS "${CVMFS2_LD_FLAGS} -ldl -lm")
include_directories (cvmfs ${INCLUDE_DIRECTORIES})

#
# do the job!
#
# for every target you can define a <targetname>_CFLAGS and <targetname>_LD_FLAGS
# variable to pass some target specific build options.
#

if (BUILD_CVMFS)
  add_executable (cvmfs2 ${CVMFS_LOADER_SOURCES})
  add_library (cvmfs_fuse_debug SHARED  ${CVMFS2_DEBUG_SOURCES})
  add_library (cvmfs_fuse SHARED ${CVMFS2_SOURCES})
  add_executable (cvmfs_fsck ${CVMFS_FSCK_SOURCES})

  if (LIBFUSE_BUILTIN)
    add_dependencies (cvmfs_fuse_debug libfuse) # here it does not matter if libfuse or osxfuse
    add_dependencies (cvmfs_fuse libfuse)       # at this point only the appropriate one is defined
  endif (LIBFUSE_BUILTIN)

  if (LIBCURL_BUILTIN)
    add_dependencies (cvmfs_fuse_debug libcares libcurl)
    add_dependencies (cvmfs_fuse libcares libcurl)
  endif (LIBCURL_BUILTIN)

  if (PACPARSER_BUILTIN)
    add_dependencies (cvmfs_fuse_debug libpacparser)
    add_dependencies (cvmfs_fuse libpacparser)
  endif (PACPARSER_BUILTIN)

  if (SQLITE3_BUILTIN)
    add_dependencies (cvmfs_fuse_debug sqlite3)
    add_dependencies (cvmfs_fuse sqlite3)
  endif (SQLITE3_BUILTIN)

  if (ZLIB_BUILTIN)
    add_dependencies (cvmfs_fuse_debug zlib)
    add_dependencies (cvmfs_fuse zlib)
    add_dependencies (cvmfs_fsck zlib)
  endif (ZLIB_BUILTIN)

  if (SPARSEHASH_BUILTIN)
    add_dependencies (cvmfs_fuse_debug sparsehash)
    add_dependencies (cvmfs_fuse sparsehash)
  endif (SPARSEHASH_BUILTIN)

  if (LEVELDB_BUILTIN)
    add_dependencies (cvmfs_fuse_debug libleveldb)
    add_dependencies (cvmfs_fuse libleveldb)
  endif (LEVELDB_BUILTIN)

  add_dependencies (cvmfs_fuse_debug libsha3)
  add_dependencies (cvmfs_fuse libsha3)
  add_dependencies (cvmfs2 libsha3)
  add_dependencies (cvmfs_fsck libsha3)

  # set some properties
  set (CVMFS_LOADER_LD_FLAGS "${CVMFS_LOADER_LD_FLAGS} -ldl -lm")
  set_target_properties (cvmfs2 PROPERTIES COMPILE_FLAGS "${CVMFS_LOADER_CFLAGS}" LINK_FLAGS "${CVMFS_LOADER_LD_FLAGS}")
  set_target_properties (cvmfs_fuse_debug PROPERTIES COMPILE_FLAGS "${CVMFS2_DEBUG_CFLAGS}" LINK_FLAGS "${CVMFS2_LD_FLAGS} ${CVMFS2_DEBUG_LD_FLAGS}")
  set_target_properties (cvmfs_fuse PROPERTIES COMPILE_FLAGS "${CVMFS2_CFLAGS}" LINK_FLAGS "${CVMFS2_LD_FLAGS}")
  set_target_properties (cvmfs_fsck PROPERTIES COMPILE_FLAGS "${CVMFS_FSCK_CFLAGS}" LINK_FLAGS "${CVMFS_FSCK_LD_FLAGS}")

  set_target_properties (cvmfs_fuse PROPERTIES VERSION ${CernVM-FS_VERSION_STRING})
  set_target_properties (cvmfs_fuse_debug PROPERTIES VERSION ${CernVM-FS_VERSION_STRING})

  # link the stuff (*_LIBRARIES are dynamic link libraries *_archive are static link libraries ... one of them will be empty for each dependency)
  set (CVMFS_FUSE_LINK_LIBRARIES ${SQLITE3_LIBRARY} ${CARES_LIBRARIES}
         ${CURL_LIBRARIES} ${LIBCURL_ARCHIVE} ${PACPARSER_LIBRARIES}
         ${LEVELDB_LIBRARIES} ${OPENSSL_LIBRARIES} ${FUSE_LIBRARIES}
         ${LIBFUSE_ARCHIVE} ${SQLITE3_ARCHIVE} ${ZLIB_LIBRARIES}
         ${PACPARSER_ARCHIVE} ${LEVELDB_ARCHIVE} ${CARES_ARCHIVE}
         ${ZLIB_ARCHIVE} ${RT_LIBRARY} ${UUID_LIBRARIES}
         ${SHA3_ARCHIVE} pthread dl)

  target_link_libraries (cvmfs2 ${CVMFS_LOADER_LIBS} ${OPENSSL_LIBRARIES}
                                ${LIBFUSE} ${RT_LIBRARY} ${UUID_LIBRARIES}
                                ${SHA3_ARCHIVE} pthread dl)
  target_link_libraries (cvmfs_fuse_debug ${CVMFS2_DEBUG_LIBS} ${CVMFS_FUSE_LINK_LIBRARIES})
  target_link_libraries (cvmfs_fuse       ${CVMFS2_LIBS} ${CVMFS_FUSE_LINK_LIBRARIES})
  target_link_libraries (cvmfs_fsck       ${CVMFS_FSCK_LIBS} ${ZLIB_LIBRARIES}
                                          ${OPENSSL_LIBRARIES} ${ZLIB_ARCHIVE}
                                          ${SHA3_ARCHIVE} ${RT_LIBRARY} pthread)

endif (BUILD_CVMFS)

if (BUILD_LIBCVMFS)
  # libcvmfs_only.a is a static lib of cvmfs without externals
  # libcvmfs.a includes the externals as well
  add_library (cvmfs_only STATIC ${LIBCVMFS_SOURCES})
  set (LIBCVMFS_LIBS ${LIBCVMFS_LIBS} ${CMAKE_CURRENT_BINARY_DIR}/libcvmfs_only.a)

  if (LIBCURL_BUILTIN)
    add_dependencies (cvmfs_only libcares libcurl)
    set (LIBCVMFS_LIBS ${CARES_ARCHIVE} ${LIBCVMFS_LIBS} )
    set (LIBCVMFS_LIBS ${LIBCURL_ARCHIVE} ${LIBCVMFS_LIBS} )
  endif (LIBCURL_BUILTIN)

  if (PACPARSER_BUILTIN)
    add_dependencies (cvmfs_only libpacparser)
    set (LIBCVMFS_LIBS ${PACPARSER_ARCHIVE} ${LIBCVMFS_LIBS} )
  endif (PACPARSER_BUILTIN)

  if (SQLITE3_BUILTIN)
    add_dependencies (cvmfs_only sqlite3)
    set (LIBCVMFS_LIBS ${SQLITE3_ARCHIVE}  ${LIBCVMFS_LIBS} )
  endif (SQLITE3_BUILTIN)

  if (ZLIB_BUILTIN)
    add_dependencies (cvmfs_only zlib)
    set (LIBCVMFS_LIBS ${ZLIB_ARCHIVE} ${LIBCVMFS_LIBS} )
  endif (ZLIB_BUILTIN)

  if (SPARSEHASH_BUILTIN)
    add_dependencies (cvmfs_only sparsehash)
  endif (SPARSEHASH_BUILTIN)

  add_dependencies (cvmfs_only libsha3)
  set (LIBCVMFS_LIBS ${SHA3_ARCHIVE} ${LIBCVMFS_LIBS})

  set_target_properties (cvmfs_only PROPERTIES COMPILE_FLAGS "${LIBCVMFS_CFLAGS}")

  if (NOT MACOSX)
    # We do not (yet) have a recipe for localizing private symbols
    # in Mac OS X, so skip that step on Mac
    set (LIBCVMFS_PUBLIC_SYMS "-public" "${CMAKE_CURRENT_SOURCE_DIR}/libcvmfs_public_syms.txt")
  endif (NOT MACOSX)

  add_custom_command(
    OUTPUT libcvmfs.a
    COMMAND ${CMAKE_CURRENT_SOURCE_DIR}/combine_libs ${LIBCVMFS_PUBLIC_SYMS} libcvmfs.a ${LIBCVMFS_LIBS}
    WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
    DEPENDS ${LIBCVMFS_LIBS}
  )

  # dummy target to cause merged libcvmfs to be produced
  add_custom_target (libcvmfs ALL DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/libcvmfs.a)
  add_dependencies (libcvmfs cvmfs_only)

  add_executable( test_libcvmfs ${TEST_LIBCVMFS_SOURCES} )
  target_link_libraries( test_libcvmfs ${CMAKE_CURRENT_BINARY_DIR}/libcvmfs.a ${SQLITE3_LIBRARY} ${CARES_LIBRARIES} ${CURL_LIBRARIES} ${PACPARSER_LIBRARIES} ${ZLIB_LIBRARIES} ${OPENSSL_LIBRARIES} ${RT_LIBRARY} ${UUID_LIBRARIES} pthread dl )
  add_dependencies (test_libcvmfs libcvmfs)

endif (BUILD_LIBCVMFS)

if (BUILD_SERVER)
  # create targets with corresponding sources and static link libraries
  add_executable (cvmfs_swissknife ${CVMFS_SWISSKNIFE_SOURCES})
  add_executable (cvmfs_suid_helper ${CVMFS_SUID_HELPER_SOURCES})

  if (GEOIP_BUILTIN)
    add_custom_target (geoip ALL)
    add_dependencies (python-geoip libgeoip)
    add_dependencies (geoip python-geoip)
  endif (GEOIP_BUILTIN)

  add_dependencies (cvmfs_swissknife libvjson)

  if (LIBCURL_BUILTIN)
    add_dependencies (cvmfs_swissknife libcares libcurl)
  endif (LIBCURL_BUILTIN)

  if (SQLITE3_BUILTIN)
    add_dependencies (cvmfs_swissknife sqlite3)
  endif (SQLITE3_BUILTIN)

  if (ZLIB_BUILTIN)
    add_dependencies (cvmfs_swissknife zlib)
  endif (ZLIB_BUILTIN)

  if (TBB_PRIVATE_LIB)
    add_dependencies (cvmfs_swissknife libtbb)
  endif (TBB_PRIVATE_LIB)

  add_dependencies (cvmfs_swissknife libsha3)

  set_target_properties (cvmfs_swissknife PROPERTIES COMPILE_FLAGS "${CVMFS_SWISSKNIFE_CFLAGS}" LINK_FLAGS "${CVMFS_SWISSKNIFE_LD_FLAGS}")

  # link the stuff (*_LIBRARIES are dynamic link libraries)
  target_link_libraries (cvmfs_swissknife  ${CVMFS_SWISSKNIFE_LIBS}
                         ${SQLITE3_LIBRARY}  ${CURL_LIBRARIES} ${LIBCURL_ARCHIVE}
                         ${CARES_LIBRARIES} ${CARES_ARCHIVE} ${TBB_LIBRARIES}
                         ${ZLIB_LIBRARIES} ${ZLIB_ARCHIVE} ${OPENSSL_LIBRARIES}
                         ${SQLITE3_ARCHIVE} ${RT_LIBRARY} ${VJSON_ARCHIVE}
                         ${SHA3_ARCHIVE} ${CAP_LIBRARIES} pthread dl)

  if (BUILD_SERVER_DEBUG)
    add_executable (cvmfs_swissknife_debug ${CVMFS_SWISSKNIFE_DEBUG_SOURCES})

    if (LIBCURL_BUILTIN)
      add_dependencies (cvmfs_swissknife_debug libcares libcurl)
    endif (LIBCURL_BUILTIN)

    if (SQLITE3_BUILTIN)
      add_dependencies (cvmfs_swissknife_debug sqlite3)
    endif (SQLITE3_BUILTIN)

    if (ZLIB_BUILTIN)
      add_dependencies (cvmfs_swissknife_debug zlib)
    endif (ZLIB_BUILTIN)

    if (TBB_PRIVATE_LIB)
      add_dependencies (cvmfs_swissknife_debug libtbb)
    endif (TBB_PRIVATE_LIB)

    add_dependencies (cvmfs_swissknife_debug libsha3)

    set_target_properties (cvmfs_swissknife_debug PROPERTIES COMPILE_FLAGS "${CVMFS_SWISSKNIFE_DEBUG_CFLAGS}" LINK_FLAGS "${CVMFS_SWISSKNIFE_DEBUG_LD_FLAGS}")

    # link the stuff (*_LIBRARIES are dynamic link libraries)
    if (NOT TBB_DEBUG_LIBRARIES)
      message (WARNING "Debug libraries of TBB were not found. Using the release versions instead.")
      set (TBB_DEBUG_LIBRARIES ${TBB_LIBRARIES})
    endif (NOT TBB_DEBUG_LIBRARIES)
    target_link_libraries (cvmfs_swissknife_debug  ${CVMFS_SWISSKNIFE_LIBS}
                           ${SQLITE3_LIBRARY}  ${CURL_LIBRARIES} ${LIBCURL_ARCHIVE}
                           ${CARES_LIBRARIES} ${CARES_ARCHIVE} ${TBB_DEBUG_LIBRARIES}
                           ${ZLIB_LIBRARIES} ${ZLIB_ARCHIVE} ${OPENSSL_LIBRARIES}
                           ${SQLITE3_ARCHIVE} ${RT_LIBRARY} ${VJSON_ARCHIVE}
                           ${SHA3_ARCHIVE} ${CAP_LIBRARIES} pthread dl)
  endif (BUILD_SERVER_DEBUG)
endif (BUILD_SERVER)

if (BUILD_PRELOADER)
  add_executable (cvmfs_preload_bin ${CVMFS_PRELOADER_SOURCES})

  if (LIBCURL_BUILTIN)
    add_dependencies (cvmfs_preload_bin libcares libcurl)
    set (PRELOADER_LIBS ${CARES_ARCHIVE} ${PRELOADER_LIBS} )
    set (PRELOADER_LIBS ${LIBCURL_ARCHIVE} ${PRELOADER_LIBS} )
  endif (LIBCURL_BUILTIN)

  if (PACPARSER_BUILTIN)
    add_dependencies (cvmfs_preload_bin libpacparser)
    set (PRELOADER_LIBS ${PACPARSER_ARCHIVE} ${PRELOADER_LIBS} )
  endif (PACPARSER_BUILTIN)

  if (SQLITE3_BUILTIN)
    add_dependencies (cvmfs_preload_bin sqlite3)
    set (PRELOADER_LIBS ${SQLITE3_ARCHIVE}  ${PRELOADER_LIBS} )
  endif (SQLITE3_BUILTIN)

  if (ZLIB_BUILTIN)
    add_dependencies (cvmfs_preload_bin zlib)
    set (PRELOADER_LIBS ${ZLIB_ARCHIVE} ${PRELOADER_LIBS} )
  endif (ZLIB_BUILTIN)

  if (SPARSEHASH_BUILTIN)
    add_dependencies (cvmfs_preload_bin sparsehash)
  endif (SPARSEHASH_BUILTIN)

  if (TBB_PRIVATE_LIB)
    add_dependencies (cvmfs_preload_bin libtbb)
  endif (TBB_PRIVATE_LIB)

  add_dependencies (cvmfs_preload_bin libsha3)
  add_dependencies (cvmfs_preload_bin libvjson)

  target_link_libraries(cvmfs_preload_bin ${SQLITE3_LIBRARY} ${CARES_LIBRARIES}
                        ${CURL_LIBRARIES} ${LIBCURL_ARCHIVE} ${ZLIB_LIBRARIES}
                        ${OPENSSL_LIBRARIES} ${CARES_ARCHIVE} ${SQLITE3_ARCHIVE}
                        ${ZLIB_ARCHIVE} ${TBB_LIBRARIES} ${RT_LIBRARY}
                        ${VJSON_ARCHIVE} ${SHA3_ARCHIVE}
                        pthread dl)
endif (BUILD_PRELOADER)

#
# Generate a bash self-extracting script for the cvmfs_preload target
#
if (BUILD_PRELOADER)
  add_custom_command (
    TARGET cvmfs_preload_bin POST_BUILD
    COMMAND ${CMAKE_CURRENT_SOURCE_DIR}/cvmfs_preload_builder.sh ${CMAKE_CURRENT_BINARY_DIR} ${CMAKE_CURRENT_SOURCE_DIR}
    COMMENT "Generating the cvmfs_preload self-extracting script"
  )
endif (BUILD_PRELOADER)

#
# installation
#

if (BUILD_CVMFS)
  install (
    TARGETS      cvmfs2 cvmfs_fsck
    RUNTIME
    DESTINATION    bin
  )

  install (
    TARGETS      cvmfs_fuse cvmfs_fuse_debug
    LIBRARY
    DESTINATION    ${CMAKE_INSTALL_LIBDIR}
  )

  install (
    FILES      cvmfs_talk cvmfs_config
    DESTINATION    bin
    PERMISSIONS    OWNER_READ OWNER_EXECUTE GROUP_READ GROUP_EXECUTE WORLD_READ WORLD_EXECUTE
  )

  if(INSTALL_BASH_COMPLETION)
    install (
        FILES         bash_completion/cvmfs.bash_completion
        RENAME        cvmfs
        DESTINATION   ${SYSCONF_INSTALL_DIR}/bash_completion.d
        PERMISSIONS   OWNER_READ OWNER_WRITE GROUP_READ WORLD_READ
    )
  endif (INSTALL_BASH_COMPLETION)

endif (BUILD_CVMFS)

if (BUILD_LIBCVMFS)
  install (
    FILES      ${CMAKE_CURRENT_BINARY_DIR}/libcvmfs.a
    DESTINATION    ${CMAKE_INSTALL_LIBDIR}
  )
  install (
    FILES                   libcvmfs.h
    DESTINATION             include
  )
endif (BUILD_LIBCVMFS)

if (BUILD_SERVER)
  install (
    TARGETS     cvmfs_swissknife
    RUNTIME
    DESTINATION    bin
  )

  install (
    TARGETS     cvmfs_suid_helper
    RUNTIME
    DESTINATION    bin
    PERMISSIONS    OWNER_READ OWNER_EXECUTE GROUP_READ GROUP_EXECUTE WORLD_READ WORLD_EXECUTE SETUID
  )

  if (BUILD_SERVER_DEBUG)
    install (
      TARGETS     cvmfs_swissknife_debug
      RUNTIME
      DESTINATION   bin
    )
  endif (BUILD_SERVER_DEBUG)

  install (
    FILES      cvmfs_server
    DESTINATION    bin
    PERMISSIONS    OWNER_READ OWNER_EXECUTE GROUP_READ GROUP_EXECUTE WORLD_READ WORLD_EXECUTE
  )

  install(
    FILES      cvmfs_server_hooks.sh.demo
    DESTINATION    "${SYSCONF_INSTALL_DIR}/cvmfs"
    PERMISSIONS    OWNER_READ OWNER_WRITE GROUP_READ WORLD_READ
  )

  install (
    FILES      cvmfs_rsync
    DESTINATION    bin
    PERMISSIONS    OWNER_READ OWNER_EXECUTE GROUP_READ GROUP_EXECUTE WORLD_READ WORLD_EXECUTE
  )

  install(
    FILES      webapi/cvmfs-api.wsgi
    DESTINATION    "/var/www/wsgi-scripts"
    PERMISSIONS    OWNER_READ OWNER_EXECUTE GROUP_READ GROUP_EXECUTE WORLD_READ WORLD_EXECUTE
  )

  if (GEOIP_BUILTIN)
    install(
      FILES      ${PYTHON_GEOIP_BUILTIN_LOCATION}/dist/GeoIP.so
      DESTINATION    "/usr/share/cvmfs-server/webapi"
      PERMISSIONS    OWNER_READ GROUP_READ WORLD_READ
    )
  endif (GEOIP_BUILTIN)

  install(
    FILES      webapi/cvmfs_api.py webapi/cvmfs_geo.py
    DESTINATION    "/usr/share/cvmfs-server/webapi"
    PERMISSIONS    OWNER_READ GROUP_READ WORLD_READ
  )

  install(
    FILES          doc/README-spooler
    DESTINATION    "/var/spool/cvmfs"
    PERMISSIONS    OWNER_READ OWNER_WRITE GROUP_READ WORLD_READ
    RENAME         README
  )

  install(
    CODE  "FILE(MAKE_DIRECTORY \$ENV{DESTDIR}/var/lib/cvmfs-server/geo)"
  )

endif (BUILD_SERVER)

if (BUILD_PRELOADER)
  install(
    FILES      ${CMAKE_CURRENT_BINARY_DIR}/cvmfs_preload
    DESTINATION    bin
    PERMISSIONS    OWNER_READ OWNER_WRITE OWNER_EXECUTE GROUP_READ GROUP_EXECUTE WORLD_READ WORLD_EXECUTE
  )
endif (BUILD_PRELOADER)
